CREATE OR REPLACE package PACK_IMPORTA_DATI is
    -- costante che identifica l'identificativo titolo possesso di asservimento
    knIdTitoloPossessoAsservimento CONSTANT NUMBER(1):=5;
    -- costante identificativo procedimento UMA
    knIdProcedimentoUma            CONSTANT NUMBER(1):=1;
    -- codice reperimento numero minimo di capi in soccida per acquisizione in allevamanto UMA
    kvCodParametroQuantitaCapiMin  CONSTANT DB_PARAMETRO.ID_PARAMETRO%TYPE:='UMSO';



    PROCEDURE  IMPORTA_DATI(P_ID_DITTA_UMA                   IN     DB_DITTA_UMA.ID_DITTA_UMA%TYPE,
                               P_ID_UTENTE_AGGIORNAMENTO     IN        DB_DITTA_UMA.EXT_ID_UTENTE_AGGIORNAMENTO%TYPE,
                            P_IMPORTA_SOCI              IN     VARCHAR2, 
                             P_CODERR                      IN OUT VARCHAR2,
                            P_MSGERR                     IN OUT VARCHAR2);

    /*********************************************************************
    Data una stringa SQL in input la esegue tramite EXECUTE IMMEDIATE
    Tipo:  funzione
    input: pSqlStr
    output: nessuno
    ritorno: 0 OK, 1 KO
    *********************************************************************/
    FUNCTION EseguiComandoSql (pSqlStr IN VARCHAR2) RETURN INTEGER;

  FUNCTION DecodeColturaUma(pIdCatalogoMatrice  DB_R_CATALOGO_MATRICE.ID_CATALOGO_MATRICE%TYPE,
                            pFlagIrrigabile     DB_STORICO_PARTICELLA.FLAG_IRRIGABILE%TYPE,
                            pDataRiferimento    DATE,
                            pIdColturaUma       DB_TIPO_COLTURA.ID_COLTURA%TYPE) RETURN DB_TIPO_COLTURA.ID_COLTURA%TYPE;

  FUNCTION SupUtilRicP26(P_ID_AZIENDA                 DB_DITTA_UMA.EXT_ID_AZIENDA%TYPE,
                         P_ID_PARTICELLA              DB_STORICO_PARTICELLA.ID_PARTICELLA%TYPE,
                         P_ID_PARTICELLA_CERTIFICATA  DB_PARTICELLA_CERTIFICATA.ID_PARTICELLA_CERTIFICATA%TYPE,
                         P_SUPERFICIE_UTILIZZATA      DB_UTILIZZO_DICHIARATO.SUPERFICIE_UTILIZZATA%TYPE,
                         P_ID_CATALOGO_MATRICE        DB_UTILIZZO_DICHIARATO.ID_CATALOGO_MATRICE%TYPE,
                         P_PERC_RIDUZIONE             NUMBER,
                         P_SECONDARIO                 VARCHAR2,
                         P_CODICE_FOTOGRAFIA_TERRENI  DB_UTILIZZO_DICHIARATO.CODICE_FOTOGRAFIA_TERRENI%TYPE) RETURN NUMBER;
END;
/


CREATE OR REPLACE package body PACK_IMPORTA_DATI is
EsciRountine       EXCEPTION;
ERRORE              EXCEPTION;
S_COM_ISTAT_COMUNE    DB_COMUNI_TERRENI.EXT_COMUNE_ISTAT%TYPE;
S_COM_TITOLO_POSSESSO DB_TIPO_TITOLO_POSSESSO.ID_TITOLO_POSSESSO%TYPE;

--P_COD_ERR     '0' ELABORAZIONE OK
--             '1' ELABORAZIONE CON ERRORE

/*********************************************************************
Effettua un attesa del numero di secondi passato in input prima
di proseguire con l'istruzzione PL/SQL successiva
Tipo:    procedure
Input:   nNumeroSecondi
Output:  pMessaggio
Return:  nessuno
********************************************************************/
PROCEDURE Sleep  (nNumeroSecondi IN INTEGER) IS
    nIntInSecondi INTERVAL DAY TO SECOND;
    dData         DATE;
BEGIN
    dData := SYSDATE;
    LOOP
        nIntInSecondi := (SYSDATE - dData) DAY TO SECOND;
        IF TO_NUMBER(EXTRACT(SECOND FROM nIntInSecondi)) >= nNumeroSecondi THEN
           EXIT;
        END IF;
    END LOOP;
END Sleep;

/*********************************************************************
Ricerca il codice parametro nella tavola DB_PARAMENTRO e ne
espone il valore
Tipo:    function
input:  pCodParamentro
output: nessuno
ritorno: VARCHAR2
*********************************************************************/
FUNCTION SelectValoreParametro (pCodParametro IN VARCHAR2) RETURN VARCHAR2 IS
    vValParametro DB_PARAMETRO.VALORE%TYPE;
BEGIN

    SELECT VALORE
      INTO vValParametro
      FROM DB_PARAMETRO
     WHERE ID_PARAMETRO = pCodParametro;

    RETURN vValParametro;

END SelectValoreParametro;

/*********************************************************************
Se sono presenti records su DITTA_FORZATA per la ditta uma e
l'anno passati in input mi restituisce 'S' altrimenti 'N'
Tipo:   function
input:  pIdDittaUma, pAnnoRif
output: nessuno
ritorno: 'S' / 'N'
*********************************************************************/
FUNCTION IsDittaUmaForzata (pIdDittaUma IN DB_CONTROLLO_DOMANDA.ID_DITTA_UMA%TYPE,
                            pAnnoRif    IN DB_CONTROLLO_DOMANDA.ANNO_ASSEGNAZIONE%TYPE
                            ) RETURN VARCHAR2 IS
    nNumRec INTEGER:=0;
    vRet    VARCHAR2(1):='N';
BEGIN

    SELECT COUNT(ID_DITTA_FORZATA)
      INTO nNumRec
      FROM DITTA_FORZATA
     WHERE ID_DITTA_UMA = pIdDittaUma
       AND ANNO_RIFERIMENTO = pAnnoRif;

    IF nNumRec > 0 THEN
       vRet := 'S';
    END IF;

    RETURN vRet;

END IsDittaUmaForzata;

FUNCTION CARICA_ALLEVAMENTI(P_ID_AZIENDA                  IN DB_DITTA_UMA.EXT_ID_AZIENDA%TYPE,
                             P_ID_DITTA_UMA                IN DB_DITTA_UMA.ID_DITTA_UMA%TYPE,
                             P_DATA_CONSISTENZA          IN DB_DICHIARAZIONE_CONSISTENZA.DATA%TYPE,
                            P_ID_CONSISTENZA          IN DB_DICHIARAZIONE_CONSISTENZA.ID_DICHIARAZIONE_CONSISTENZA%TYPE,
                            P_ID_UTENTE_AGGIORNAMENTO IN DB_DOMANDA_ASSEGNAZIONE.EXT_ID_UTENTE_AGGIORNAMENTO%TYPE,
                            P_MSGERR                   IN OUT VARCHAR2,
                            P_CODERR                   IN OUT VARCHAR2) RETURN BOOLEAN IS
        vDittaForzata   VARCHAR2(1):='N';

        -- modificato cursore con aggiunta tabella DB_CATEG_ANIMALE_UMA_SMRGAA
        -- ed eliminazione DECODE su C.ID_CATEGORIA_ANIMALE
        -- inoltre seleziono anche gli allevamenti con FLAG_SOCCIDA = 'S'
        CURSOR C_ALLEV (pNumMinCapi IN INTEGER) IS
        SELECT A.ISTAT_COMUNE,
               CA.ID_CATEGORIA_ANIMALE AS CATEGORIA,
               DECODE(CA.DIVISORE_QUANTITA_CAPI, NULL, C.QUANTITA,ROUND(C.QUANTITA/CA.DIVISORE_QUANTITA_CAPI,0)) AS QUANTITA,
               A.CODICE_AZIENDA_ZOOTECNICA,
               DECODE(A.FLAG_SOCCIDA,'S','CAPI IN SOCCIDA',NULL) AS DESC_TIPO_ALLEVAMENTO,FLAG_SOCCIDA
          FROM SMRGAA.DB_ALLEVAMENTO A,
               SMRGAA.DB_UTE B,
               SMRGAA.DB_CATEGORIE_ALLEVAMENTO C,
               DB_CATEG_ANIMALE_UMA_SMRGAA CA
         WHERE B.ID_AZIENDA = P_ID_AZIENDA
           AND ((B.DATA_FINE_ATTIVITA IS NULL AND P_DATA_CONSISTENZA >= B.DATA_INIZIO_ATTIVITA) OR
                (B.DATA_FINE_ATTIVITA IS NOT NULL AND P_DATA_CONSISTENZA BETWEEN B.DATA_INIZIO_ATTIVITA AND B.DATA_FINE_ATTIVITA))
           AND A.ID_UTE = B.ID_UTE
           AND A.ID_ALLEVAMENTO = C.ID_ALLEVAMENTO
           AND NVL(A.FLAG_SOCCIDA,'N') = 'N' 
           AND ((A.DATA_FINE IS NULL AND P_DATA_CONSISTENZA >= A.DATA_INIZIO) OR
                (A.DATA_FINE IS NOT NULL AND P_DATA_CONSISTENZA BETWEEN A.DATA_INIZIO AND A.DATA_FINE))
           AND C.ID_CATEGORIA_ANIMALE = CA.EXT_ID_CATEGORIA_ANIMALE
           AND C.QUANTITA             > 0  
           AND P_DATA_CONSISTENZA BETWEEN CA.DATA_INIZIO_VALIDITA AND NVL(CA.DATA_FINE_VALIDITA,P_DATA_CONSISTENZA)
           and   substr(a.istat_comune, 1, 3) in ('076', '077')


        UNION ALL

        SELECT A.ISTAT_COMUNE,
               CA.ID_CATEGORIA_ANIMALE AS CATEGORIA,
               DECODE(CA.DIVISORE_QUANTITA_CAPI, NULL, C.QUANTITA,ROUND(C.QUANTITA/CA.DIVISORE_QUANTITA_CAPI,0)) AS QUANTITA,
               A.CODICE_AZIENDA_ZOOTECNICA,
               DECODE(A.FLAG_SOCCIDA,'S','CAPI IN SOCCIDA',NULL) AS DESC_TIPO_ALLEVAMENTO,FLAG_SOCCIDA
          FROM SMRGAA.DB_ALLEVAMENTO A,
               SMRGAA.DB_UTE B,
               SMRGAA.DB_CATEGORIE_ALLEVAMENTO C,
               DB_CATEG_ANIMALE_UMA_SMRGAA CA
         WHERE B.ID_AZIENDA = P_ID_AZIENDA
           AND ((B.DATA_FINE_ATTIVITA IS NULL AND P_DATA_CONSISTENZA >= B.DATA_INIZIO_ATTIVITA) OR
                (B.DATA_FINE_ATTIVITA IS NOT NULL AND P_DATA_CONSISTENZA BETWEEN B.DATA_INIZIO_ATTIVITA AND B.DATA_FINE_ATTIVITA))
           AND A.ID_UTE = B.ID_UTE
           AND A.ID_ALLEVAMENTO = C.ID_ALLEVAMENTO
           AND NVL(A.FLAG_SOCCIDA,'N') = 'S'
           AND ((A.DATA_FINE IS NULL AND P_DATA_CONSISTENZA >= A.DATA_INIZIO) OR
                (A.DATA_FINE IS NOT NULL AND P_DATA_CONSISTENZA BETWEEN A.DATA_INIZIO AND A.DATA_FINE))
           AND C.ID_CATEGORIA_ANIMALE = CA.EXT_ID_CATEGORIA_ANIMALE
           AND C.QUANTITA             > 0  
           AND P_DATA_CONSISTENZA BETWEEN CA.DATA_INIZIO_VALIDITA AND NVL(CA.DATA_FINE_VALIDITA,P_DATA_CONSISTENZA)
           -- verifica che il proprietario dei capi sia un allevatore
           AND A.CODICE_FISCALE_PROPRIETARIO IS NOT NULL
           -- verifica che il totale dei capi dell'allevamento, indipendentemente
           -- dalla categoria, sia maggiore di un numero minimo dato in input
           AND (SELECT SUM(C.QUANTITA)
                  FROM SMRGAA.DB_CATEGORIE_ALLEVAMENTO
                 WHERE ID_ALLEVAMENTO = C.ID_ALLEVAMENTO) > pNumMinCapi
           -- verifica che il proprietario dei capi abbia un allevamento
           -- valido alla sysdate e caricato in anagrafe per azienda valida
           AND (EXISTS
                 (SELECT AL.ID_ALLEVAMENTO
                   FROM DB_ANAGRAFICA_AZIENDA AA,
                        SMRGAA.DB_ALLEVAMENTO AL,
                        DB_DICHIARAZIONE_CONSISTENZA DC,
                        DB_UTE UT
                  WHERE AA.ID_AZIENDA = DC.ID_AZIENDA
                    AND AA.ID_AZIENDA = UT.ID_AZIENDA
                    AND UT.ID_UTE = AL.ID_UTE
                    AND AA.DATA_FINE_VALIDITA IS NULL
                    AND AA.CUAA = A.CODICE_FISCALE_PROPRIETARIO
                    AND UT.DATA_INIZIO_ATTIVITA <= DC.DATA_INSERIMENTO_DICHIARAZIONE
                    AND NVL(UT.DATA_FINE_ATTIVITA, SYSDATE) >= DC.DATA_INSERIMENTO_DICHIARAZIONE
                    AND AL.DATA_INIZIO <= DC.DATA_INSERIMENTO_DICHIARAZIONE
                    AND NVL(AL.DATA_FINE, SYSDATE) >= DC.DATA_INSERIMENTO_DICHIARAZIONE
                    AND DC.DATA =
                        (SELECT MAX(DC1.DATA)
                           FROM DB_DICHIARAZIONE_CONSISTENZA DC1, DB_TIPO_MOTIVO_DICHIARAZIONE TMD
                          WHERE TMD.ID_MOTIVO_DICHIARAZIONE = DC1.ID_MOTIVO_DICHIARAZIONE
                            AND DC1.ID_AZIENDA = AA.ID_AZIENDA
                            AND TMD.TIPO_DICHIARAZIONE <> 'C')) OR vDittaForzata = 'S')
            and   substr(a.istat_comune, 1, 3) in ('076', '077');

        S_NOTE          DB_ALLEVAMENTO.NOTE%TYPE;
        nNumMinCapi     INTEGER:=0;
        nIdAllevamento  DB_ALLEVAMENTO.ID_ALLEVAMENTO%TYPE;

BEGIN
    vDittaForzata := IsDittaUmaForzata (P_ID_DITTA_UMA, TO_CHAR(SYSDATE,'YYYY'));

    BEGIN
        nNumMinCapi := SelectValoreParametro (kvCodParametroQuantitaCapiMin);
    EXCEPTION
        WHEN NO_DATA_FOUND THEN
            P_CODERR := '1';
            P_MSGERR := 'Parametro ' || kvCodParametroQuantitaCapiMin || ' non trovato su DB_PARAMETRO';
            RAISE ERRORE;
        WHEN OTHERS THEN
            P_CODERR := '1';
            P_MSGERR := 'Parametro ' || kvCodParametroQuantitaCapiMin || ' in formato erroreo effettuare verifica su DB_PARAMETRO';
            RAISE ERRORE;
    END;

    FOR REC_ALLEV IN C_ALLEV (nNumMinCapi) LOOP
        IF REC_ALLEV.CODICE_AZIENDA_ZOOTECNICA IS NOT NULL THEN
            S_NOTE  := 'CODICE AZIENDA ZOOTECNICA: ' || REC_ALLEV.CODICE_AZIENDA_ZOOTECNICA;
        ELSE
            S_NOTE  := NULL;
        END IF;
        -- aggiungo la dicitura nelle NOTE
        IF REC_ALLEV.DESC_TIPO_ALLEVAMENTO IS NOT NULL THEN
            IF S_NOTE IS NOT NULL THEN
               S_NOTE := S_NOTE || ' - ' || REC_ALLEV.DESC_TIPO_ALLEVAMENTO;
            ELSE
               S_NOTE := REC_ALLEV.DESC_TIPO_ALLEVAMENTO;
            END IF;
        END IF;

        INSERT INTO DB_ALLEVAMENTO (
                ID_ALLEVAMENTO,
                ID_DITTA_UMA,
                ID_CATEGORIA_ANIMALE,
                DATA_CARICO,
                QUANTITA,
                DATA_INIZIO_VALIDITA,
                NOTE,
                EXT_ID_UTENTE_AGGIORNAMENTO,
                DATA_AGGIORNAMENTO,
                EXT_ID_CONSISTENZA,
                DATA_CONSISTENZA,
                FLAG_SOCCIDA)  
        VALUES (SEQ_ALLEVAMENTO.NEXTVAL,
                P_ID_DITTA_UMA,
                REC_ALLEV.CATEGORIA,
                P_DATA_CONSISTENZA,
                REC_ALLEV.QUANTITA,
                SYSDATE,
                S_NOTE,
                P_ID_UTENTE_AGGIORNAMENTO,
                SYSDATE,
                P_ID_CONSISTENZA,
                P_DATA_CONSISTENZA,
                REC_ALLEV.FLAG_SOCCIDA)
        RETURNING ID_ALLEVAMENTO INTO nIdAllevamento;

        -- aggiunto caricamento delle lavorazioni praticate
        -- prese eventualmente da quelle degli allevamenti della dichiarazione di consistenza
        -- precedente
        INSERT INTO DB_LAVORAZIONI_PRATICATE
            (
            ID_LAVORAZIONI_PRATICATE,
            ID_ALLEVAMENTO,
            ID_LITRI_ALLEVAMENTO,
            EXT_ID_UTENTE_AGGIORNAMENTO,
            DATA_AGGIORNAMENTO
            )
        SELECT SEQ_LAVORAZIONI_PRATICATE.nextval,
               nIdAllevamento,
               ID_LITRI_ALLEVAMENTO,
               P_ID_UTENTE_AGGIORNAMENTO,
               SYSDATE
          FROM (SELECT DISTINCT TLA_NEW.ID_LITRI_ALLEVAMENTO AS ID_LITRI_ALLEVAMENTO
                  FROM DB_ALLEVAMENTO A,
                       DB_LAVORAZIONI_PRATICATE LP,
                       DB_TIPO_LITRI_ALLEVAMENTO TLA_OLD,
                       DB_TIPO_LITRI_ALLEVAMENTO TLA_NEW
                 WHERE A.ID_DITTA_UMA = P_ID_DITTA_UMA
                   AND A.ID_CATEGORIA_ANIMALE = REC_ALLEV.CATEGORIA
                   AND A.EXT_ID_CONSISTENZA = (SELECT MAX(EXT_ID_CONSISTENZA)
                                                 FROM DB_ALLEVAMENTO
                                                WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
                                                  AND EXT_ID_CONSISTENZA <> P_ID_CONSISTENZA)
                   AND A.DATA_SCARICO IS NULL
                   AND A.ID_ALLEVAMENTO = LP.ID_ALLEVAMENTO
                   AND LP.ID_LITRI_ALLEVAMENTO = TLA_OLD.ID_LITRI_ALLEVAMENTO
                   and TLA_NEW.ID_CATEGORIA_ANIMALE = TLA_OLD.ID_CATEGORIA_ANIMALE
                   and TLA_NEW.ID_LAVORAZIONI = TLA_OLD.ID_LAVORAZIONI
                   and TLA_NEW.DATA_FINE_VALIDITA is null
                   );


    END LOOP;

    RETURN (TRUE);

exception
 when ERRORE then
      RETURN (FALSE);
 when others then
    P_CODERR := '1';
    P_MSGERR := 'ERRORE CARICA_ALLEVAMENTI: '|| SQLERRM;
    RETURN (FALSE);
END CARICA_ALLEVAMENTI;

FUNCTION CARICA_SERRE(P_ID_AZIENDA              IN DB_DITTA_UMA.EXT_ID_AZIENDA%TYPE,
                       P_ID_DITTA_UMA            IN DB_DITTA_UMA.ID_DITTA_UMA%TYPE,
                       P_DATA_CONSISTENZA        IN DB_DICHIARAZIONE_CONSISTENZA.DATA%TYPE,
                      P_ID_CONSISTENZA            IN DB_DICHIARAZIONE_CONSISTENZA.ID_DICHIARAZIONE_CONSISTENZA%TYPE,
                      P_ID_UTENTE_AGGIORNAMENTO IN DB_DOMANDA_ASSEGNAZIONE.EXT_ID_UTENTE_AGGIORNAMENTO%TYPE,
                      P_MSGERR                     IN OUT VARCHAR2,
                      P_CODERR                     IN OUT VARCHAR2) RETURN BOOLEAN IS

S_NOTE  DB_SERRA.NOTE%TYPE;

CURSOR C_SERRE IS

       SELECT DECODE(A.ID_FORMA_FABBRICATO, 1,1, 2,2, 3,3, 4,4, 5,1, 6,2, 7,3, 8,4) FORMA_FABBRICATO,
       A.DIMENSIONE, A.LUNGHEZZA, A.LARGHEZZA, A.ALTEZZA, A.DATA_INIZIO_VALIDITA,
       DECODE(A.ID_COLTURA_SERRA, 1, 36, 2, 37) COLTURA, A.MESI_RISCALDAMENTO_SERRA, A.ORE_RISCALDAMENTO_SERRA
          FROM SMRGAA.DB_FABBRICATO A, SMRGAA.DB_UTE B
       WHERE B.ID_AZIENDA=P_ID_AZIENDA
          AND ((B.DATA_FINE_ATTIVITA IS NULL
       AND P_DATA_CONSISTENZA+1 >= B.DATA_INIZIO_ATTIVITA) OR (B.DATA_FINE_ATTIVITA IS NOT NULL
       AND P_DATA_CONSISTENZA+1 BETWEEN B.DATA_INIZIO_ATTIVITA AND B.DATA_FINE_ATTIVITA))
       AND A.ID_UTE=B.ID_UTE
       AND A.ID_TIPOLOGIA_FABBRICATO = 23
       AND ((A.DATA_FINE_VALIDITA IS NULL AND P_DATA_CONSISTENZA+1 >= A.DATA_INIZIO_VALIDITA)
       OR (A.DATA_FINE_VALIDITA IS NOT NULL AND P_DATA_CONSISTENZA+1 BETWEEN A.DATA_INIZIO_VALIDITA AND A.DATA_FINE_VALIDITA));

BEGIN

     S_NOTE := 'Serre riscaldate importate dal fasciolo aziendale. Dichiarazione del ';

     FOR REC_SERRE IN C_SERRE LOOP

            INSERT INTO DB_SERRA (ID_SERRA, ID_DITTA_UMA, ID_FORMA_SERRA, ID_COLTURA, DATA_CARICO,
            DATA_SCARICO, LUNGHEZZA, LARGHEZZA, ALTEZZA, VOLUME_METRI_CUBI, MESI_DI_RISCALDAMENTO,
            ORE_DI_RISCALDAMENTO_ANNUALI, NOTE, DATA_INIZIO_VALIDITA, EXT_ID_UTENTE_AGGIORNAMENTO,
            DATA_FINE_VALIDITA, DATA_AGGIORNAMENTO, MODIFICA_INTERMEDIARIO, EXT_ID_CONSISTENZA, DATA_CONSISTENZA)
               VALUES (SEQ_SERRA.NEXTVAL, P_ID_DITTA_UMA, REC_SERRE.FORMA_FABBRICATO, nvl(REC_SERRE.COLTURA, 37),
            REC_SERRE.DATA_INIZIO_VALIDITA, NULL, REC_SERRE.LUNGHEZZA, REC_SERRE.LARGHEZZA, REC_SERRE.ALTEZZA, REC_SERRE.DIMENSIONE,
            nvl(REC_SERRE.MESI_RISCALDAMENTO_SERRA, 4), nvl(REC_SERRE.ORE_RISCALDAMENTO_SERRA, 2880),
            S_NOTE || TO_CHAR(P_DATA_CONSISTENZA, 'DD/MM/YYYY'), REC_SERRE.DATA_INIZIO_VALIDITA,
            P_ID_UTENTE_AGGIORNAMENTO, NULL, SYSDATE, NULL, P_ID_CONSISTENZA, P_DATA_CONSISTENZA);

     END LOOP;


     RETURN (TRUE);

exception
 when ERRORE then
      RETURN (FALSE);
 when others then
    P_CODERR := '1';
    P_MSGERR := 'ERRORE CARICA_SERRE: '|| SQLERRM;
    RETURN (FALSE);
END CARICA_SERRE;

FUNCTION CaricaMacchineGaa(pIdAzienda       DB_DITTA_UMA.EXT_ID_AZIENDA%TYPE,
                           pIdDittaUma      DB_DITTA_UMA.ID_DITTA_UMA%TYPE,
                           pIdUtente        DB_DOMANDA_ASSEGNAZIONE.EXT_ID_UTENTE_AGGIORNAMENTO%TYPE,
                           pMsgErr      OUT VARCHAR2,
                           pCodErr      OUT VARCHAR2) RETURN BOOLEAN IS 

  TYPE tblIdMacchina IS TABLE OF DB_UTILIZZO.ID_MACCHINA%TYPE INDEX BY BINARY_INTEGER;

  IdMacchina    tblIdMacchina;
  nIdMacchina   DB_MACCHINA.ID_MACCHINA%TYPE;
  nCont         SIMPLE_INTEGER := 0;
  nIdUtilizzo   DB_UTILIZZO.ID_UTILIZZO%TYPE;
  vIdProvincia  DB_DITTA_UMA.EXT_PROVINCIA_UMA%TYPE;
  vDittaUma     DB_DITTA_UMA.DITTA_UMA%TYPE;
  dDataCarico   SMRGAA.DB_POSSESSO_MACCHINA.DATA_CARICO%TYPE;
  dDataScarico  SMRGAA.DB_POSSESSO_MACCHINA.DATA_SCARICO%TYPE;
  nIdScarico    SMRGAA.DB_POSSESSO_MACCHINA.ID_SCARICO%TYPE;
BEGIN
  SELECT DISTINCT U.ID_MACCHINA
  BULK COLLECT INTO IdMacchina
  FROM   DB_UTILIZZO U,DB_MACCHINA M,DB_DATI_MACCHINA DM,DB_TIPO_GENERE_MACCHINA TGM
  WHERE  U.ID_DITTA_UMA         = pIdDittaUma
  AND    M.ID_MACCHINA          = U.ID_MACCHINA
  AND    M.ID_MACCHINA          = DM.ID_MACCHINA
  AND    TGM.ID_GENERE_MACCHINA = DM.ID_GENERE_MACCHINA
  AND    TGM.FLAG_IMPORTABILE   = 'S';

  FOR i IN 1..IdMacchina.COUNT LOOP
    DELETE DB_MOVIMENTI_TARGA
    WHERE  ID_MACCHINA = IdMacchina(i);

    DELETE DB_NUMERO_TARGA
    WHERE  ID_MACCHINA = IdMacchina(i);

    DELETE DB_POSSESSO
    WHERE  ID_UTILIZZO IN (SELECT ID_UTILIZZO
                           FROM   DB_UTILIZZO
                           WHERE  ID_MACCHINA = IdMacchina(i));

    DELETE DB_UTILIZZO
    WHERE  ID_MACCHINA = IdMacchina(i);

    DELETE DB_DATI_MACCHINA
    WHERE  ID_MACCHINA = IdMacchina(i);

    DELETE DB_MACCHINA
    WHERE  ID_MACCHINA = IdMacchina(i);
  END LOOP;

  FOR recMacch IN (SELECT M.*,TM.DESCRIZIONE,TM.MATRICE,TC.ID_CATEGORIA ID_CATEGORIA_UMA
                   FROM   SMRGAA.DB_MACCHINA M, DB_TIPO_GENERE_MACCHINA TGM, SMRGAA.DB_TIPO_MARCA TM,
                          DB_TIPO_CATEGORIA TC 
                   WHERE  TGM.FLAG_IMPORTABILE   = 'S'
                   AND    ((M.ID_CATEGORIA IS NOT NULL AND TC.ID_CATEGORIA       = M.ID_CATEGORIA) OR
                           (M.ID_CATEGORIA IS NULL     AND TC.ID_GENERE_MACCHINA = M.ID_GENERE_MACCHINA AND 
                            TC.FLAG_CATEGORIA_GENERICA = 'S'))  
                   AND    TGM.ID_GENERE_MACCHINA = M.ID_GENERE_MACCHINA
                   AND    TM.ID_MARCA(+)         = M.ID_MARCA
                   AND    EXISTS                 (SELECT 'X'
                                                  FROM   SMRGAA.DB_POSSESSO_MACCHINA PM,DB_UTE U
                                                  WHERE  PM.ID_MACCHINA = M.ID_MACCHINA
                                                  AND    PM.ID_UTE      = U.ID_UTE
                                                  AND    U.ID_AZIENDA   = pIdAzienda)) LOOP
    IF recMacch.ID_MARCA IS NOT NULL THEN
      SELECT COUNT(*)
      INTO   nCont
      FROM   DB_TIPO_MARCA
      WHERE  ID_MARCA = recMacch.ID_MARCA;

      IF nCont = 0 THEN
        INSERT INTO DB_TIPO_MARCA
        (ID_MARCA, DESCRIZIONE, MATRICE)
        VALUES
        (recMacch.ID_MARCA, recMacch.DESCRIZIONE, recMacch.MATRICE);
      END IF;
    END IF;

    INSERT INTO DB_MACCHINA
    (ID_MACCHINA, ID_MATRICE, MATRICOLA_TELAIO, MATRICOLA_MOTORE, DATA_AGGIORNAMENTO, EXT_ID_UTENTE_AGGIORNAMENTO, 
     EXT_ID_MACCHINA_GAA, ANNO_COSTRUZIONE, NOTE)
    VALUES
    (SEQ_MACCHINA.NEXTVAL,NULL,recMacch.MATRICOLA_TELAIO,recMacch.MATRICOLA_MOTORE,SYSDATE,pIdUtente,
     recMacch.ID_MACCHINA, recMacch.ANNO_COSTRUZIONE, recMacch.NOTE)
    RETURNING ID_MACCHINA INTO nIdMacchina;

    INSERT INTO DB_DATI_MACCHINA
    (ID_DATI_MACCHINA, ID_MACCHINA, MARCA, ID_GENERE_MACCHINA, 
     TIPO_MACCHINA,ID_CATEGORIA, TARA, LORDO,NUMERO_ASSI, 
     CALORIE, EXT_ID_UTENTE_AGGIORNAMENTO,POTENZA, DATA_AGGIORNAMENTO, ID_ALIMENTAZIONE,
     ID_NAZIONALITA, ID_TRAZIONE,QUANTITA)
    VALUES
    (SEQ_DATI_MACCHINA.NEXTVAL,nIdMacchina,recMacch.DESCRIZIONE, recMacch.ID_GENERE_MACCHINA, 
     recMacch.TIPO_MACCHINA,recMacch.ID_CATEGORIA_UMA, recMacch.TARA, recMacch.LORDO,recMacch.NUMERO_ASSI, 
     recMacch.CALORIE, pIdUtente,recMacch.POTENZA_KW, SYSDATE, recMacch.ID_ALIMENTAZIONE,
     recMacch.ID_NAZIONALITA, recMacch.ID_TRAZIONE,recMacch.QUANTITA);

    SELECT EXT_PROVINCIA_UMA,DITTA_UMA
    INTO   vIdProvincia,vDittaUma
    FROM   DB_DITTA_UMA
    WHERE  ID_DITTA_UMA = pIdDittaUma;

    INSERT INTO DB_MOVIMENTI_TARGA
    (ID_MOVIMENTI_TARGA, ID_NUMERO_TARGA, ID_MACCHINA, ID_MOVIMENTAZIONE,ID_PROVINCIA, DITTA_UMA, 
     PROTOCOLLO_MODELLO_49, NUMERO_MODELLO_49, DATA_INIZIO_VALIDITA, DATA_FINE_VALIDITA, DATA_PROTOCOLLO_49, 
     DATA_AGGIORNAMENTO, EXT_ID_UTENTE_AGGIORNAMENTO, ANNO_MODELLO_49)
    VALUES
    (SEQ_MOVIMENTI_TARGA.NEXTVAL,NULL,nIdMacchina,11,vIdProvincia,vDittaUma,
     NULL,NULL,SYSDATE,NULL,NULL,
     SYSDATE,pIdUtente,NULL);

    SELECT MIN(PM.DATA_CARICO),MAX(PM.DATA_SCARICO),MAX(PM.ID_SCARICO)
    INTO   dDataCarico,dDataScarico,nIdScarico
    FROM   SMRGAA.DB_POSSESSO_MACCHINA PM,DB_UTE U,DB_DITTA_UMA DU
    WHERE  PM.ID_UTE       = U.ID_UTE
    AND    U.ID_AZIENDA    = DU.EXT_ID_AZIENDA
    AND    PM.ID_MACCHINA  = recMacch.ID_MACCHINA
    AND    PM.DATA_CARICO <= NVL(DU.DATA_CESSAZIONE,SYSDATE);

    INSERT INTO DB_UTILIZZO
    (ID_UTILIZZO, ID_DITTA_UMA, ID_MACCHINA, DATA_CARICO, DATA_SCARICO,ID_SCARICO, EXT_ID_UTENTE_AGGIORNAMENTO,
     DATA_AGGIORNAMENTO)
    VALUES
    (SEQ_UTILIZZO.NEXTVAL,pIdDittaUma, nIdMacchina, dDataCarico, dDataScarico,nIdScarico, pIdUtente, 
     SYSDATE)
    RETURNING ID_UTILIZZO INTO nIdUtilizzo;

    FOR recPoss IN (SELECT PM.*,DU.ID_DITTA_UMA
                    FROM   SMRGAA.DB_POSSESSO_MACCHINA PM,DB_UTE U,DB_DITTA_UMA DU
                    WHERE  PM.ID_UTE       = U.ID_UTE
                    AND    U.ID_AZIENDA    = DU.EXT_ID_AZIENDA
                    AND    PM.ID_MACCHINA  = recMacch.ID_MACCHINA
                    AND    PM.DATA_CARICO <= NVL(DU.DATA_CESSAZIONE,SYSDATE)) LOOP

      INSERT INTO DB_POSSESSO
      (ID_POSSESSO, ID_UTILIZZO, ID_FORMA_POSSESSO, DATA_SCADENZA_LEASING, EXT_ID_AZIENDA, DATA_INIZIO_VALIDITA, 
       DATA_FINE_VALIDITA, EXT_ID_UTENTE_AGGIORNAMENTO, DATA_AGGIORNAMENTO, PERCENTUALE_POSSESSO)
      VALUES
      (SEQ_POSSESSO.NEXTVAL,nIdUtilizzo, recPoss.ID_TIPO_FORMA_POSSESSO, recPoss.DATA_SCADENZA_LEASING, 
       recPoss.ID_AZIENDA_LEASING, recPoss.DATA_INIZIO_VALIDITA,recPoss.DATA_FINE_VALIDITA, pIdUtente, SYSDATE, 
       recPoss.PERCENTUALE_POSSESSO);
    END LOOP;
  END LOOP;

  RETURN TRUE;
EXCEPTION
  WHEN OTHERS THEN
    pCodErr := '1';
    pMsgErr := 'ERRORE CaricaMacchineGaa: '|| SQLERRM||' RIGA = '||dbms_utility.FORMAT_ERROR_BACKTRACE;
    RETURN FALSE;  
END CaricaMacchineGaa;                             

FUNCTION DecodeColturaUma(pIdCatalogoMatrice  DB_R_CATALOGO_MATRICE.ID_CATALOGO_MATRICE%TYPE,
                          pFlagIrrigabile     DB_STORICO_PARTICELLA.FLAG_IRRIGABILE%TYPE,
                          pDataRiferimento    DATE,
                          pIdColturaUma       DB_TIPO_COLTURA.ID_COLTURA%TYPE) RETURN DB_TIPO_COLTURA.ID_COLTURA%TYPE IS                           

  nIdColtura  DB_TIPO_COLTURA.ID_COLTURA%TYPE;
BEGIN
  BEGIN
    SELECT ID_COLTURA
    INTO   nIdColtura
    FROM   DB_R_CATALOGO_MATRICE_UMA_IRR
    WHERE  EXT_ID_CATALOGO_MATRICE  = pIdCatalogoMatrice   
    AND    FLAG_IRRIGABILE          = pFlagIrrigabile
    AND    (DATA_FINE_VALIDITA      IS NULL OR DATA_FINE_VALIDITA >= NVL(pDataRiferimento,SYSDATE))
    AND    DATA_INIZIO_VALIDITA    <= NVL(pDataRiferimento,SYSDATE);
  EXCEPTION
    WHEN NO_DATA_FOUND THEN
      nIdColtura := pIdColturaUma;
  END;

  RETURN nIdColtura;
END DecodeColturaUma;

FUNCTION SupUtilRicP26(P_ID_AZIENDA                 DB_DITTA_UMA.EXT_ID_AZIENDA%TYPE,
                       P_ID_PARTICELLA              DB_STORICO_PARTICELLA.ID_PARTICELLA%TYPE,
                       P_ID_PARTICELLA_CERTIFICATA  DB_PARTICELLA_CERTIFICATA.ID_PARTICELLA_CERTIFICATA%TYPE,
                       P_SUPERFICIE_UTILIZZATA      DB_UTILIZZO_DICHIARATO.SUPERFICIE_UTILIZZATA%TYPE,
                       P_ID_CATALOGO_MATRICE        DB_UTILIZZO_DICHIARATO.ID_CATALOGO_MATRICE%TYPE,
                       P_PERC_RIDUZIONE             NUMBER,
                       P_SECONDARIO                 VARCHAR2,
                       P_CODICE_FOTOGRAFIA_TERRENI  DB_UTILIZZO_DICHIARATO.CODICE_FOTOGRAFIA_TERRENI%TYPE) RETURN NUMBER IS 

  nP26                   NUMBER;
  nSupElegg              NUMBER;
  nSupUtilPerColturaGis  NUMBER;
BEGIN
  RETURN P_SUPERFICIE_UTILIZZATA;

  nP26 := SMRGAA.PCK_SMRGAA_LIBRERIA.CALCOLA_P26(P_ID_AZIENDA,P_ID_PARTICELLA,P_ID_PARTICELLA_CERTIFICATA);                         

  -- se la particella non e' in P26
  IF nP26 = 0 THEN
    RETURN P_SUPERFICIE_UTILIZZATA;
  END IF;

  -- restituisce la superficie eleggibile dell'intera coltura gis compatibile
  nSupElegg := SMRGAA.PCK_SMRGAA_LIBRERIA.SELTOTSUPELEGBYPARTEVETCAT(P_ID_PARTICELLA_CERTIFICATA,
                                                                     P_ID_CATALOGO_MATRICE);

  IF nSupElegg = 0 THEN
    RETURN 0;
  END IF;

  IF P_SECONDARIO = 'N' THEN
    SELECT NVL(SUM(NVL(SUPERFICIE_UTILIZZATA,0)),0)
    INTO   nSupUtilPerColturaGis 
    FROM   DB_UTILIZZO_DICHIARATO UD,DB_CONDUZIONE_DICHIARATA CD 
    WHERE  UD.ID_CATALOGO_MATRICE       = P_ID_CATALOGO_MATRICE
    AND    CD.ID_PARTICELLA             = P_ID_PARTICELLA
    AND    CD.ID_CONDUZIONE_DICHIARATA  = UD.ID_CONDUZIONE_DICHIARATA
    AND    UD.CODICE_FOTOGRAFIA_TERRENI = P_CODICE_FOTOGRAFIA_TERRENI;
  ELSE
    SELECT NVL(SUM(NVL(SUP_UTILIZZATA_SECONDARIA,0)),0) END   
    INTO   nSupUtilPerColturaGis 
    FROM   DB_UTILIZZO_DICHIARATO UD,DB_CONDUZIONE_DICHIARATA CD
    WHERE  UD.ID_CATALOGO_MATRICE_SECONDARIO = P_ID_CATALOGO_MATRICE
    AND    CD.ID_PARTICELLA                  = P_ID_PARTICELLA
    AND    CD.ID_CONDUZIONE_DICHIARATA       = UD.ID_CONDUZIONE_DICHIARATA
    AND    UD.CODICE_FOTOGRAFIA_TERRENI      = P_CODICE_FOTOGRAFIA_TERRENI;
  END IF;

  RETURN ((P_SUPERFICIE_UTILIZZATA * (LEAST(nSupUtilPerColturaGis,nSupElegg) * 100 / nSupElegg) / 100) * P_PERC_RIDUZIONE) /100;
  --RETURN ((nSupElegg * (P_SUPERFICIE_UTILIZZATA * 100 / nSupUtilPerColturaGis) / 100) * P_PERC_RIDUZIONE) /100; 
END SupUtilRicP26;  


FUNCTION CARICA_TERRENI(P_ID_AZIENDA                   DB_DITTA_UMA.EXT_ID_AZIENDA%TYPE,
                        P_ID_DITTA_UMA                 DB_DITTA_UMA.ID_DITTA_UMA%TYPE,
                        P_ID_CONSISTENZA               DB_DOMANDA_ASSEGNAZIONE.EXT_ID_CONSISTENZA%TYPE,
                        P_DATA_CONSISTENZA             DB_DICHIARAZIONE_CONSISTENZA.DATA%TYPE,
                        P_ID_UTENTE_AGGIORNAMENTO      DB_DOMANDA_ASSEGNAZIONE.EXT_ID_UTENTE_AGGIORNAMENTO%TYPE,
                        P_IMPORTA_SOCI                 VARCHAR2, 
                        P_SECONDARIE                   NUMBER, 
                        P_MSGERR                   OUT VARCHAR2,
                        P_CODERR                   OUT VARCHAR2) RETURN BOOLEAN IS

  N_ID_SUPERFICIE         DB_SUPERFICIE_AZIENDA.ID_SUPERFICIE_AZIENDA%TYPE;
  N_ID_COLTURA_PRATICATA  DB_COLTURA_PRATICATA.ID_COLTURA_PRATICATA%TYPE;
  nPercRiduzione          NUMBER;
  nContAcconto            SIMPLE_INTEGER := 0;  

  CURSOR C_SUPERF(pIdConsistenza  DB_DOMANDA_ASSEGNAZIONE.EXT_ID_CONSISTENZA%TYPE,
                  pIdAzienda      DB_DITTA_UMA.EXT_ID_AZIENDA%TYPE) IS
    WITH SUPERFICI_UMIZZATE AS (
    SELECT E.COMUNE,
           CASE WHEN (P_IMPORTA_SOCI = 'S' AND P_ID_AZIENDA != pIdAzienda) THEN 11
           ELSE TPUG.ID_TITOLO_POSSESSO
           END ID_TITOLO_POSSESSO_UMA,
           DecodeColturaUma(C.ID_CATALOGO_MATRICE,E.FLAG_IRRIGABILE,NULL,CMU.ID_COLTURA) ID_COLTURA_UMA,
           SupUtilRicP26(pIdAzienda,E.ID_PARTICELLA,NULL,C.SUPERFICIE_UTILIZZATA,
                         C.ID_CATALOGO_MATRICE,nPercRiduzione,'N',A.CODICE_FOTOGRAFIA_TERRENI) SUPERFICIE_UTILIZZATA,
           E.ID_STORICO_PARTICELLA,CMU.EXT_ID_CATALOGO_MATRICE,CM.ID_VARIETA,
           CM.ID_UTILIZZO
    FROM   DB_DICHIARAZIONE_CONSISTENZA A,DB_CONDUZIONE_DICHIARATA B,DB_R_TITOLO_POSSESSO_UMA_GAA TPUG,
           DB_UTILIZZO_DICHIARATO C,DB_STORICO_PARTICELLA E,DB_R_CATALOGO_MATRICE_UMA CMU,COMUNE I,
           PROVINCIA L,DB_R_CATALOGO_MATRICE CM
    WHERE  A.CODICE_FOTOGRAFIA_TERRENI = B.CODICE_FOTOGRAFIA_TERRENI
    AND    B.ID_STORICO_PARTICELLA     = E.ID_STORICO_PARTICELLA 
    AND    (((B.ID_TITOLO_POSSESSO <> knIdTitoloPossessoAsservimento) AND 
             (P_IMPORTA_SOCI = 'S' AND P_ID_AZIENDA = pIdAzienda AND B.ID_TITOLO_POSSESSO <> 6)) OR
            ((B.ID_TITOLO_POSSESSO <> knIdTitoloPossessoAsservimento) AND 
             (P_IMPORTA_SOCI = 'N' OR P_ID_AZIENDA != pIdAzienda)))
    AND    B.ID_TITOLO_POSSESSO           = TPUG.EXT_ID_TITOLO_POSSESSO
    AND    TPUG.DATA_FINE_VALIDITA        IS NULL
    AND    B.ID_CONDUZIONE_DICHIARATA     = C.ID_CONDUZIONE_DICHIARATA
    AND    A.ID_DICHIARAZIONE_CONSISTENZA = pIdConsistenza
    AND    CMU.EXT_ID_CATALOGO_MATRICE    = C.ID_CATALOGO_MATRICE
    AND    CMU.EXT_ID_CATALOGO_MATRICE    = CM.ID_CATALOGO_MATRICE
    AND    I.ISTAT_COMUNE                 = E.COMUNE
    AND    L.ISTAT_PROVINCIA              = I.ISTAT_PROVINCIA
    AND    L.ID_REGIONE                   = '17'
    AND    CMU.DATA_FINE_VALIDITA IS NULL
    /*AND    SMRGAA.PCK_SMRGAA_LIBRERIA.Calcola_P30(E.ID_STORICO_PARTICELLA,NULL) = 0 
    AND    SMRGAA.PCK_SMRGAA_LIBRERIA.Calcola_P25(E.ID_STORICO_PARTICELLA,NULL) = 0
    AND    ((SMRGAA.PCK_SMRGAA_LIBRERIA.CALCOLA_P26(P_ID_AZIENDA,E.ID_PARTICELLA,PC.ID_PARTICELLA_CERTIFICATA) > 0 AND
             nContAcconto > 0 AND
             SMRGAA.PCK_SMRGAA_LIBRERIA.SELTOTSUPELEGBYPARTEVETCAT(PC.ID_PARTICELLA_CERTIFICATA,C.ID_CATALOGO_MATRICE) > 0) OR
            (SMRGAA.PCK_SMRGAA_LIBRERIA.CALCOLA_P26(P_ID_AZIENDA,E.ID_PARTICELLA,PC.ID_PARTICELLA_CERTIFICATA) = 0))*/) 
    SELECT COMUNE,ID_TITOLO_POSSESSO_UMA,ID_COLTURA_UMA,SUM(SUPERFICIE_UTILIZZATA) SUM_SUP_UTIL
    FROM   SUPERFICI_UMIZZATE
    GROUP BY COMUNE, ID_TITOLO_POSSESSO_UMA,ID_COLTURA_UMA
    ORDER BY COMUNE, ID_TITOLO_POSSESSO_UMA,ID_COLTURA_UMA;


    CURSOR C_SUPERF_SEC(pIdConsistenza  DB_DOMANDA_ASSEGNAZIONE.EXT_ID_CONSISTENZA%TYPE,
                        pIdAzienda      DB_DITTA_UMA.EXT_ID_AZIENDA%TYPE) IS
    WITH SUPERFICI_UMIZZATE AS (
    SELECT 
           DecodeColturaUma(C.ID_CATALOGO_MATRICE_SECONDARIO,E.FLAG_IRRIGABILE,NULL,CMU.ID_COLTURA) ID_COLTURA_UMA,
           E.ID_STORICO_PARTICELLA,
           CMU.EXT_ID_CATALOGO_MATRICE,
           CM.ID_VARIETA,
           CM.ID_UTILIZZO,
           SA.ID_SUPERFICIE_AZIENDA,
           SupUtilRicP26(pIdAzienda,E.ID_PARTICELLA,NULL,C.SUP_UTILIZZATA_SECONDARIA,
                         C.ID_CATALOGO_MATRICE_SECONDARIO,nPercRiduzione,'N',A.CODICE_FOTOGRAFIA_TERRENI) SUP_UTILIZZATA_SECONDARIA
      FROM DB_DICHIARAZIONE_CONSISTENZA A,
           DB_CONDUZIONE_DICHIARATA B,
           DB_R_TITOLO_POSSESSO_UMA_GAA TPUG,
           DB_UTILIZZO_DICHIARATO C,
           DB_STORICO_PARTICELLA E,
           DB_R_CATALOGO_MATRICE_UMA CMU, 
           COMUNE I,
           PROVINCIA L,
           DB_SUPERFICIE_AZIENDA SA,DB_COMUNI_TERRENI CT,DB_R_CATALOGO_MATRICE CM
     WHERE SA.EXT_ID_AZIENDA = pIdAzienda
     AND   SA.ID_DITTA_UMA   = P_ID_DITTA_UMA
     AND   CT.ID_SUPERFICIE_AZIENDA = SA.ID_SUPERFICIE_AZIENDA
     AND   CT.EXT_COMUNE_ISTAT      = E.COMUNE
     AND   SA.DATA_FINE_VALIDITA    IS NULL
     AND   SA.DATA_SCARICO          IS NULL
     AND   SA.ID_TITOLO_POSSESSO = (CASE WHEN (P_IMPORTA_SOCI = 'S' AND P_ID_AZIENDA != pIdAzienda) THEN 11
           ELSE TPUG.ID_TITOLO_POSSESSO
           END )
     AND A.CODICE_FOTOGRAFIA_TERRENI = B.CODICE_FOTOGRAFIA_TERRENI
       AND B.ID_STORICO_PARTICELLA = E.ID_STORICO_PARTICELLA 
       --AND E.DATA_FINE_VALIDITA IS NULL 
       AND (((B.ID_TITOLO_POSSESSO <> knIdTitoloPossessoAsservimento)
       and (P_IMPORTA_SOCI = 'S' AND P_ID_AZIENDA = pIdAzienda and B.ID_TITOLO_POSSESSO <> 6))
       OR ((B.ID_TITOLO_POSSESSO <> knIdTitoloPossessoAsservimento)
       and (P_IMPORTA_SOCI = 'N' OR P_ID_AZIENDA != pIdAzienda)))
       AND B.ID_TITOLO_POSSESSO = TPUG.EXT_ID_TITOLO_POSSESSO
       AND TPUG.DATA_FINE_VALIDITA IS NULL
       AND B.ID_CONDUZIONE_DICHIARATA = C.ID_CONDUZIONE_DICHIARATA
       AND A.ID_DICHIARAZIONE_CONSISTENZA = pIdConsistenza
       AND CMU.EXT_ID_CATALOGO_MATRICE = C.ID_CATALOGO_MATRICE_SECONDARIO
       AND CMU.EXT_ID_CATALOGO_MATRICE = CM.ID_CATALOGO_MATRICE
       AND CMU.DATA_FINE_VALIDITA IS NULL
       AND I.ISTAT_COMUNE =E.COMUNE
       AND L.ISTAT_PROVINCIA = I.ISTAT_PROVINCIA
       AND L.ID_REGIONE = '17'
       /*AND SMRGAA.PCK_SMRGAA_LIBRERIA.CALCOLA_P26(P_ID_AZIENDA,E.ID_PARTICELLA,PC.ID_PARTICELLA_CERTIFICATA) = 0
       AND SMRGAA.PCK_SMRGAA_LIBRERIA.Calcola_P30(E.ID_STORICO_PARTICELLA,NULL) = 0 
       AND SMRGAA.PCK_SMRGAA_LIBRERIA.Calcola_P25(E.ID_STORICO_PARTICELLA,NULL) = 0*/)
    SELECT ID_SUPERFICIE_AZIENDA,
           ID_COLTURA_UMA,
           SUM(SUP_UTILIZZATA_SECONDARIA) SUM_SUP_UTIL
     FROM SUPERFICI_UMIZZATE
    GROUP BY ID_SUPERFICIE_AZIENDA,ID_COLTURA_UMA
    ORDER BY ID_SUPERFICIE_AZIENDA,ID_COLTURA_UMA;

    CURSOR C_PARTICELLE (pIdColturaUma       DB_TIPO_COLTURA.ID_COLTURA%TYPE,
                         pIdConsistenza  DB_DOMANDA_ASSEGNAZIONE.EXT_ID_CONSISTENZA%TYPE,
                         pTitoloPossessoUma DB_R_TITOLO_POSSESSO_UMA_GAA.ID_TITOLO_POSSESSO%TYPE,
                         pIdAzienda      DB_DITTA_UMA.EXT_ID_AZIENDA%TYPE) IS
    WITH SUPERFICI_UMIZZATE AS (
    SELECT E.COMUNE,
           CASE WHEN (P_IMPORTA_SOCI = 'S' AND P_ID_AZIENDA != pIdAzienda) THEN 11
           ELSE TPUG.ID_TITOLO_POSSESSO
           END ID_TITOLO_POSSESSO_UMA,
           DecodeColturaUma(C.ID_CATALOGO_MATRICE,E.FLAG_IRRIGABILE,NULL,CMU.ID_COLTURA) ID_COLTURA_UMA,
           SupUtilRicP26(pIdAzienda,E.ID_PARTICELLA,NULL,C.SUPERFICIE_UTILIZZATA,
                         C.ID_CATALOGO_MATRICE,nPercRiduzione,'N',A.CODICE_FOTOGRAFIA_TERRENI) SUPERFICIE_UTILIZZATA,
           SupUtilRicP26(pIdAzienda,E.ID_PARTICELLA,NULL,C.SUPERFICIE_UTILIZZATA,
                         C.ID_CATALOGO_MATRICE,100,'N',A.CODICE_FOTOGRAFIA_TERRENI) SUP_ELEGGIBILE_COLTURA_DICH ,
           0 SUP_ELEGGIBILE_COLT_ELEG,                         
           E.ID_STORICO_PARTICELLA,
           CMU.EXT_ID_CATALOGO_MATRICE,
           CM.ID_VARIETA,
           CM.ID_UTILIZZO
      FROM DB_DICHIARAZIONE_CONSISTENZA A,
           DB_CONDUZIONE_DICHIARATA B,
           DB_R_TITOLO_POSSESSO_UMA_GAA TPUG, 
           DB_UTILIZZO_DICHIARATO C,          
           DB_STORICO_PARTICELLA E,
           DB_R_CATALOGO_MATRICE_UMA CMU,
           COMUNE I,
           PROVINCIA L,DB_R_CATALOGO_MATRICE CM
     WHERE A.CODICE_FOTOGRAFIA_TERRENI = B.CODICE_FOTOGRAFIA_TERRENI
       AND B.ID_STORICO_PARTICELLA = E.ID_STORICO_PARTICELLA 
       --AND E.DATA_FINE_VALIDITA IS NULL 
       AND (((B.ID_TITOLO_POSSESSO <> knIdTitoloPossessoAsservimento) 
       and (P_IMPORTA_SOCI = 'S' AND P_ID_AZIENDA = pIdAzienda and B.ID_TITOLO_POSSESSO <> 6))
       OR ((B.ID_TITOLO_POSSESSO <> knIdTitoloPossessoAsservimento) 
       and (P_IMPORTA_SOCI = 'N' OR P_ID_AZIENDA != pIdAzienda)))
       AND B.ID_TITOLO_POSSESSO = TPUG.EXT_ID_TITOLO_POSSESSO
       AND TPUG.DATA_FINE_VALIDITA IS NULL
       AND B.ID_CONDUZIONE_DICHIARATA = C.ID_CONDUZIONE_DICHIARATA
       AND A.ID_DICHIARAZIONE_CONSISTENZA = pIdConsistenza
       AND CMU.EXT_ID_CATALOGO_MATRICE = C.ID_CATALOGO_MATRICE
       AND CMU.EXT_ID_CATALOGO_MATRICE = CM.ID_CATALOGO_MATRICE
       AND CMU.DATA_FINE_VALIDITA IS NULL
       AND I.ISTAT_COMUNE =E.COMUNE
       AND L.ISTAT_PROVINCIA = I.ISTAT_PROVINCIA
       AND L.ID_REGIONE = '17'
       /*AND SMRGAA.PCK_SMRGAA_LIBRERIA.Calcola_P30(E.ID_STORICO_PARTICELLA,NULL) = 0 
       AND SMRGAA.PCK_SMRGAA_LIBRERIA.Calcola_P25(E.ID_STORICO_PARTICELLA,NULL) = 0
       AND    ((SMRGAA.PCK_SMRGAA_LIBRERIA.CALCOLA_P26(P_ID_AZIENDA,E.ID_PARTICELLA,PC.ID_PARTICELLA_CERTIFICATA) > 0 AND
             nContAcconto > 0  AND
             SMRGAA.PCK_SMRGAA_LIBRERIA.SELTOTSUPELEGBYPARTEVETCAT(PC.ID_PARTICELLA_CERTIFICATA,CMU.EXT_ID_CATALOGO_MATRICE) > 0) OR
            (SMRGAA.PCK_SMRGAA_LIBRERIA.CALCOLA_P26(P_ID_AZIENDA,E.ID_PARTICELLA,PC.ID_PARTICELLA_CERTIFICATA) = 0))*/)                                                          
     SELECT ID_STORICO_PARTICELLA,
            ID_UTILIZZO,
            ID_VARIETA,
            EXT_ID_CATALOGO_MATRICE,
            SUM(SUPERFICIE_UTILIZZATA) AS SUPERFICIE_UTILIZZATA,
            SUM(SUP_ELEGGIBILE_COLTURA_DICH) SUP_ELEGGIBILE_COLTURA_DICH,
            SUM(SUP_ELEGGIBILE_COLT_ELEG) SUP_ELEGGIBILE_COLT_ELEG
       FROM SUPERFICI_UMIZZATE
       WHERE COMUNE = S_COM_ISTAT_COMUNE
         AND ID_TITOLO_POSSESSO_UMA = pTitoloPossessoUma
         AND ID_COLTURA_UMA = pIdColturaUma 
    GROUP BY ID_STORICO_PARTICELLA, ID_UTILIZZO,ID_VARIETA,EXT_ID_CATALOGO_MATRICE;


  CURSOR C_PARTICELLE_SEC (pIdColturaUma      DB_TIPO_COLTURA.ID_COLTURA%TYPE,
                           pIdConsistenza     DB_DOMANDA_ASSEGNAZIONE.EXT_ID_CONSISTENZA%TYPE,
                           pIdAzienda         DB_DITTA_UMA.EXT_ID_AZIENDA%TYPE,
                           pIdSupAzienda      DB_SUPERFICIE_AZIENDA.ID_SUPERFICIE_AZIENDA%TYPE) IS
    WITH SUPERFICI_UMIZZATE AS (
    SELECT 
           DecodeColturaUma(C.ID_CATALOGO_MATRICE_SECONDARIO,E.FLAG_IRRIGABILE,NULL,CMU.ID_COLTURA) ID_COLTURA_UMA,
           E.ID_STORICO_PARTICELLA,
           CMU.EXT_ID_CATALOGO_MATRICE,
           CM.ID_VARIETA,
           CM.ID_UTILIZZO,
                   
           SupUtilRicP26(pIdAzienda,E.ID_PARTICELLA,NULL,C.SUP_UTILIZZATA_SECONDARIA,
                         C.ID_CATALOGO_MATRICE_SECONDARIO,nPercRiduzione,'N',A.CODICE_FOTOGRAFIA_TERRENI) SUP_UTILIZZATA_SECONDARIA,
           SupUtilRicP26(pIdAzienda,E.ID_PARTICELLA,NULL,C.SUP_UTILIZZATA_SECONDARIA,
                         C.ID_CATALOGO_MATRICE_SECONDARIO,100,'N',A.CODICE_FOTOGRAFIA_TERRENI) SUP_ELEGGIBILE_COLTURA_DICH ,
           0 SUP_ELEGGIBILE_COLT_ELEG
      FROM DB_DICHIARAZIONE_CONSISTENZA A,
           DB_CONDUZIONE_DICHIARATA B,
           DB_R_TITOLO_POSSESSO_UMA_GAA TPUG, 
           DB_UTILIZZO_DICHIARATO C,          
           DB_STORICO_PARTICELLA E,
           DB_R_CATALOGO_MATRICE_UMA CMU, 
           COMUNE I,
           PROVINCIA L,
           DB_SUPERFICIE_AZIENDA SA,DB_COMUNI_TERRENI CT,DB_R_CATALOGO_MATRICE CM
     WHERE SA.ID_SUPERFICIE_AZIENDA = pIdSupAzienda
     AND   CT.ID_SUPERFICIE_AZIENDA = SA.ID_SUPERFICIE_AZIENDA
     AND   CT.EXT_COMUNE_ISTAT      = E.COMUNE
     AND   SA.ID_TITOLO_POSSESSO = (CASE WHEN (P_IMPORTA_SOCI = 'S' AND P_ID_AZIENDA != pIdAzienda) THEN 11
           ELSE TPUG.ID_TITOLO_POSSESSO
           END )
     AND A.CODICE_FOTOGRAFIA_TERRENI = B.CODICE_FOTOGRAFIA_TERRENI
       AND B.ID_STORICO_PARTICELLA = E.ID_STORICO_PARTICELLA 
       --AND E.DATA_FINE_VALIDITA IS NULL 
       AND (((B.ID_TITOLO_POSSESSO <> knIdTitoloPossessoAsservimento) 
       and (P_IMPORTA_SOCI = 'S' AND P_ID_AZIENDA = pIdAzienda and B.ID_TITOLO_POSSESSO <> 6))
       OR ((B.ID_TITOLO_POSSESSO <> knIdTitoloPossessoAsservimento) 
       and (P_IMPORTA_SOCI = 'N' OR P_ID_AZIENDA != pIdAzienda)))
       AND B.ID_TITOLO_POSSESSO = TPUG.EXT_ID_TITOLO_POSSESSO
       AND TPUG.DATA_FINE_VALIDITA IS NULL
       AND B.ID_CONDUZIONE_DICHIARATA = C.ID_CONDUZIONE_DICHIARATA
       AND A.ID_DICHIARAZIONE_CONSISTENZA = pIdConsistenza
       AND I.ISTAT_COMUNE =E.COMUNE
       AND L.ISTAT_PROVINCIA = I.ISTAT_PROVINCIA
       AND L.ID_REGIONE = '17'
       AND CMU.EXT_ID_CATALOGO_MATRICE = C.ID_CATALOGO_MATRICE_SECONDARIO
       AND CMU.EXT_ID_CATALOGO_MATRICE = CM.ID_CATALOGO_MATRICE
       AND CMU.DATA_FINE_VALIDITA IS NULL
       /*AND SMRGAA.PCK_SMRGAA_LIBRERIA.CALCOLA_P26(P_ID_AZIENDA,E.ID_PARTICELLA,PC.ID_PARTICELLA_CERTIFICATA) = 0
       AND SMRGAA.PCK_SMRGAA_LIBRERIA.Calcola_P30(E.ID_STORICO_PARTICELLA,NULL) = 0 
       AND SMRGAA.PCK_SMRGAA_LIBRERIA.Calcola_P25(E.ID_STORICO_PARTICELLA,NULL) = 0*/)
     SELECT ID_STORICO_PARTICELLA,
            ID_UTILIZZO,
            ID_VARIETA,
            EXT_ID_CATALOGO_MATRICE,
            SUM(SUP_UTILIZZATA_SECONDARIA) AS SUPERFICIE_UTILIZZATA,
            SUM(SUP_ELEGGIBILE_COLTURA_DICH) SUP_ELEGGIBILE_COLTURA_DICH,
            SUM(SUP_ELEGGIBILE_COLT_ELEG) SUP_ELEGGIBILE_COLT_ELEG
       FROM SUPERFICI_UMIZZATE
       WHERE ID_COLTURA_UMA = pIdColturaUma
    GROUP BY ID_STORICO_PARTICELLA, ID_UTILIZZO,ID_VARIETA,EXT_ID_CATALOGO_MATRICE;

  nLast                PLS_INTEGER := 0;

  TYPE recCons IS RECORD (IdCons  DB_DOMANDA_ASSEGNAZIONE.EXT_ID_CONSISTENZA%TYPE,
                          IdAz    DB_AZIENDA.ID_AZIENDA%TYPE);

  TYPE tblCons IS TABLE OF recCons INDEX BY BINARY_INTEGER;
  idCons  tblCons;
  nSuperficieUtilizzata  NUMBER;
BEGIN
  S_COM_ISTAT_COMUNE    := NULL;
  S_COM_TITOLO_POSSESSO := NULL;

  SELECT TO_NUMBER(VALORE)
  INTO   nPercRiduzione
  FROM   DB_TIPO_PARAMETRO
  WHERE  COD_PARAMETRO      = 'UMPE'
  AND    DATA_FINE_VALIDITA IS NULL; 

  SELECT COUNT(*)
  INTO   nContAcconto
  FROM   DB_DOMANDA_ASSEGNAZIONE 
  WHERE  ID_DITTA_UMA = P_ID_DITTA_UMA
  AND    TO_CHAR(DATA_RIFERIMENTO,'YYYY') = TO_CHAR(SYSDATE,'YYYY')
  AND    ID_STATO_DOMANDA                 = 35;


  IF P_IMPORTA_SOCI = 'N' THEN
    idCons(1).IdCons := P_ID_CONSISTENZA;
    idCons(1).IdAz   := P_ID_AZIENDA;
  ELSE
    SELECT MAX(DC.ID_DICHIARAZIONE_CONSISTENZA),ID_AZIENDA_ASSOCIATA
    BULK COLLECT INTO idCons
    FROM   DB_AZIENDA_COLLEGATA AC,DB_DICHIARAZIONE_CONSISTENZA DC,DB_MOTIVO_ESCLUSO_PROCEDIMENTO MEP
    WHERE  AC.ID_AZIENDA            = P_ID_AZIENDA
    AND    AC.DATA_FINE_VALIDITA    IS NULL
    AND    AC.DATA_USCITA           IS NULL
    AND    AC.ID_SOGGETTO_ASSOCIATO IS NULL
    AND    AC.ID_AZIENDA_ASSOCIATA  = DC.ID_AZIENDA
    AND    NOT EXISTS (SELECT MEP.ID_MOTIVO_ESCLUSO_PROCEDIMENTO
                                    FROM DB_MOTIVO_ESCLUSO_PROCEDIMENTO MEP
                                    WHERE DC.ID_MOTIVO_DICHIARAZIONE = MEP.ID_MOTIVO_DICHIARAZIONE
                                    AND MEP.ID_PROCEDIMENTO = knIdProcedimentoUma
                                  )
    GROUP BY ID_AZIENDA_ASSOCIATA;

    IF idCons.FIRST IS NULL THEN
      P_CODERR := '1';
      P_MSGERR := 'Attenzione: per procedere e'' necessario inserire l''elenco soci sul fascicolo aziendale oppure
                            verificare che almeno uno dei soci presenti abbia effettuato una dichiarazione di consistenza';
      RETURN FALSE;
    END IF;

    nLast := idCons.LAST;

    idCons(nLast+1).IdCons := P_ID_CONSISTENZA;
    idCons(nLast+1).IdAz   := P_ID_AZIENDA;
  END IF;


  FOR i IN idCons.FIRST..idCons.LAST LOOP
    S_COM_TITOLO_POSSESSO := NULL;
    S_COM_ISTAT_COMUNE    := NULL;

    FOR REC_SUPERF IN C_SUPERF(idCons(i).IdCons,idCons(i).IdAz) LOOP
      --a rottura di possesso e/o comune e/o contratto
      IF S_COM_TITOLO_POSSESSO <> REC_SUPERF.ID_TITOLO_POSSESSO_UMA OR
         S_COM_TITOLO_POSSESSO IS NULL                            OR
         S_COM_ISTAT_COMUNE    IS NULL                             OR
         REC_SUPERF.COMUNE        <> S_COM_ISTAT_COMUNE             THEN

        S_COM_TITOLO_POSSESSO := REC_SUPERF.ID_TITOLO_POSSESSO_UMA;
        S_COM_ISTAT_COMUNE      := REC_SUPERF.COMUNE;

        INSERT INTO DB_SUPERFICIE_AZIENDA
        (ID_SUPERFICIE_AZIENDA, ID_DITTA_UMA,DENOMINAZIONE, DATA_CARICO, DATA_SCARICO,
         ID_TITOLO_POSSESSO,
         DATA_REGISTRAZIONE, NUMERO_REGISTRAZIONE, ID_CONTRATTO,ID_SCADENZA, DATA_SCADENZA_AFFITTO, NOTE, EXT_ID_SOGGETTO,
         DATA_AGGIORNAMENTO, DATA_INIZIO_VALIDITA, EXT_ID_UTENTE_AGGIORNAMENTO,DATA_FINE_VALIDITA, MODIFICA_INTERMEDIARIO,
         EXT_ID_CONSISTENZA, DATA_CONSISTENZA,EXT_ID_AZIENDA)
          VALUES
        (SEQ_SUPERFICIE_AZIENDA.NEXTVAL, P_ID_DITTA_UMA, '-', P_DATA_CONSISTENZA, NULL,
         S_COM_TITOLO_POSSESSO,
         NULL, NULL,NULL, NULL, NULL,NULL, NULL,
         SYSDATE, SYSDATE, P_ID_UTENTE_AGGIORNAMENTO,NULL, NULL,
         idCons(i).IdCons,
         (SELECT DATA FROM DB_DICHIARAZIONE_CONSISTENZA WHERE ID_DICHIARAZIONE_CONSISTENZA = idCons(i).IdCons),
         idCons(i).IdAz)
        RETURNING ID_SUPERFICIE_AZIENDA INTO N_ID_SUPERFICIE;

        INSERT INTO DB_COMUNI_TERRENI
        (ID_COMUNI_TERRENI, ID_SUPERFICIE_AZIENDA, EXT_COMUNE_ISTAT)
        VALUES
        (SEQ_COMUNI_TERRENI.NEXTVAL, N_ID_SUPERFICIE, S_COM_ISTAT_COMUNE);

        END IF;

        INSERT INTO DB_COLTURA_PRATICATA
      (ID_COLTURA_PRATICATA, ID_SUPERFICIE_AZIENDA, ID_COLTURA,SUPERFICIE_UTILIZZATA, EXT_ID_UTENTE_AGGIORNAMENTO,
       DATA_AGGIORNAMENTO,MODIFICA_INTERMEDIARIO,FLAG_COLTURA_SECONDARIA)
      VALUES
      (SEQ_COLTURA_PRATICATA.NEXTVAL, N_ID_SUPERFICIE, REC_SUPERF.ID_COLTURA_UMA, REC_SUPERF.SUM_SUP_UTIL,P_ID_UTENTE_AGGIORNAMENTO,
       SYSDATE, NULL,'N')
      RETURNING ID_COLTURA_PRATICATA INTO N_ID_COLTURA_PRATICATA;

      FOR REC_PARTICELLE IN C_PARTICELLE (REC_SUPERF.ID_COLTURA_UMA,idCons(i).IdCons,S_COM_TITOLO_POSSESSO,idCons(i).IdAz)LOOP
        INSERT INTO DB_PARTICELLA_COLTURA
        (ID_PARTICELLA_COLTURA, ID_COLTURA_PRATICATA, EX_ID_STORICO_PARTICELLA,
         EXT_ID_UTILIZZO, EXT_ID_VARIETA,SUPERFICIE_UTILIZZATA,
         EXT_ID_CATALOGO_MATRICE,SUP_ELEGGIBILE_COLTURA_DICH,SUP_ELEGGIBILE_COLT_ELEG )
        VALUES
        (SEQ_PARTICELLA_COLTURA.NEXTVAL, N_ID_COLTURA_PRATICATA, REC_PARTICELLE.ID_STORICO_PARTICELLA,
         REC_PARTICELLE.ID_UTILIZZO,REC_PARTICELLE.ID_VARIETA,REC_PARTICELLE.SUPERFICIE_UTILIZZATA,
         REC_PARTICELLE.EXT_ID_CATALOGO_MATRICE,REC_PARTICELLE.SUP_ELEGGIBILE_COLTURA_DICH,
            REC_PARTICELLE.SUP_ELEGGIBILE_COLT_ELEG );
      END LOOP;
    END LOOP;

    IF P_SECONDARIE > 0 THEN
      FOR REC_SUPERF_SEC IN C_SUPERF_SEC(idCons(i).IdCons,idCons(i).IdAz) LOOP
        INSERT INTO DB_COLTURA_PRATICATA
        (ID_COLTURA_PRATICATA, ID_SUPERFICIE_AZIENDA, ID_COLTURA,SUPERFICIE_UTILIZZATA, EXT_ID_UTENTE_AGGIORNAMENTO,
         DATA_AGGIORNAMENTO,MODIFICA_INTERMEDIARIO,FLAG_COLTURA_SECONDARIA)
        VALUES
        (SEQ_COLTURA_PRATICATA.NEXTVAL, REC_SUPERF_SEC.ID_SUPERFICIE_AZIENDA, REC_SUPERF_SEC.ID_COLTURA_UMA, REC_SUPERF_SEC.SUM_SUP_UTIL,P_ID_UTENTE_AGGIORNAMENTO,
         SYSDATE, NULL,'S')
        RETURNING ID_COLTURA_PRATICATA INTO N_ID_COLTURA_PRATICATA;

        FOR REC_PARTICELLE_SEC IN C_PARTICELLE_SEC (REC_SUPERF_SEC.ID_COLTURA_UMA,idCons(i).IdCons,idCons(i).IdAz,
                                                    REC_SUPERF_SEC.ID_SUPERFICIE_AZIENDA) LOOP

          INSERT INTO DB_PARTICELLA_COLTURA
          (ID_PARTICELLA_COLTURA, ID_COLTURA_PRATICATA, EX_ID_STORICO_PARTICELLA,
           EXT_ID_UTILIZZO, EXT_ID_VARIETA,SUPERFICIE_UTILIZZATA,
           EXT_ID_CATALOGO_MATRICE,SUP_ELEGGIBILE_COLTURA_DICH,SUP_ELEGGIBILE_COLT_ELEG)
          VALUES
          (SEQ_PARTICELLA_COLTURA.NEXTVAL, N_ID_COLTURA_PRATICATA, REC_PARTICELLE_SEC.ID_STORICO_PARTICELLA,
           REC_PARTICELLE_SEC.ID_UTILIZZO,REC_PARTICELLE_SEC.ID_VARIETA,REC_PARTICELLE_SEC.SUPERFICIE_UTILIZZATA,
           REC_PARTICELLE_SEC.EXT_ID_CATALOGO_MATRICE,REC_PARTICELLE_SEC.SUP_ELEGGIBILE_COLTURA_DICH,
            REC_PARTICELLE_SEC.SUP_ELEGGIBILE_COLT_ELEG);
        END LOOP;
      END LOOP;
    END IF;

  END LOOP;

  RETURN TRUE;

EXCEPTION
  WHEN ERRORE THEN
    RETURN FALSE;
  WHEN OTHERS THEN
    P_CODERR := '1';
    P_MSGERR := 'ERRORE CARICA_TERRENI: '|| SQLERRM||' RIGA = '||dbms_utility.FORMAT_ERROR_BACKTRACE;
    RETURN FALSE;
END CARICA_TERRENI;

/*** MAIN ***/
PROCEDURE IMPORTA_DATI(P_ID_DITTA_UMA            IN     DB_DITTA_UMA.ID_DITTA_UMA%TYPE,
                       P_ID_UTENTE_AGGIORNAMENTO IN     DB_DITTA_UMA.EXT_ID_UTENTE_AGGIORNAMENTO%TYPE,
                       P_IMPORTA_SOCI            IN     VARCHAR2, 
                       P_CODERR                  IN OUT VARCHAR2,
                       P_MSGERR                     IN OUT VARCHAR2) IS

N_ID_AZIENDA                DB_DITTA_UMA.EXT_ID_AZIENDA%TYPE;
N_ID_CONSISTENZA_GAA        DB_DOMANDA_ASSEGNAZIONE.EXT_ID_CONSISTENZA%TYPE;
N_ID_CONSISTENZA_UMA        DB_DOMANDA_ASSEGNAZIONE.EXT_ID_CONSISTENZA%TYPE;
D_DATA_CONSISTENZA_GAA        DB_DICHIARAZIONE_CONSISTENZA.DATA%TYPE;
D_DATA_CONSISTENZA_UMA        DB_DICHIARAZIONE_CONSISTENZA.DATA%TYPE;
N_CONTATORE                    NUMBER(10);
N_ID_SUPERFICIE_AZIENDA        DB_SUPERFICIE_AZIENDA.ID_SUPERFICIE_AZIENDA%TYPE;
N_ID_ALLEVAMENTO            DB_ALLEVAMENTO.ID_ALLEVAMENTO%TYPE;
N_ID_SERRA                    DB_SERRA.ID_SERRA%TYPE;

  nAnnoPar  NUMBER;
  nCont     PLS_INTEGER;
  nContDom  PLS_INTEGER;
BEGIN

  SELECT COUNT(*)
  INTO   nContDom
  FROM   DB_DOMANDA_ASSEGNAZIONE DA
  WHERE  DA.ID_DITTA_UMA                     = P_ID_DITTA_UMA
  AND    TO_CHAR(DA.DATA_RIFERIMENTO,'YYYY') = TO_CHAR(SYSDATE,'YYYY')
  AND    DA.ID_STATO_DOMANDA                 = 30;

     P_CODERR          := '0';
     P_MSGERR          := NULL;

     BEGIN
         SELECT EXT_ID_AZIENDA
         INTO N_ID_AZIENDA
         FROM DB_DITTA_UMA
         WHERE ID_DITTA_UMA = P_ID_DITTA_UMA;
     EXCEPTION WHEN NO_DATA_FOUND THEN
       P_CODERR := '1';
       P_MSGERR := 'DITTA UMA NON TROVATA. PRATICA: ' || P_ID_DITTA_UMA ;
       RAISE ERRORE;
     WHEN OTHERS THEN
       P_CODERR := '1';
       P_MSGERR := 'ERRORE GRAVE ACCESSO DB_DITTA_UMA. CONTATTARE L''ASSISTENZA COMUNICANDO LA SEGUENTE ANOMALIA: ' || SQLERRM;
     RAISE ERRORE;
     END;

    /* Cerco in anagrafe l'ultima dichiarazione di consistenza */
    BEGIN
        SELECT ID_DICHIARAZIONE_CONSISTENZA, DATA_INSERIMENTO_DICHIARAZIONE
        INTO N_ID_CONSISTENZA_GAA, D_DATA_CONSISTENZA_GAA
        FROM DB_DICHIARAZIONE_CONSISTENZA
        WHERE ID_AZIENDA = N_ID_AZIENDA
        AND DATA = 
                 (SELECT MAX (DC.DATA) 
                    FROM DB_DICHIARAZIONE_CONSISTENZA DC
                   WHERE DC.ID_AZIENDA = N_ID_AZIENDA
                     AND NOT EXISTS (SELECT MEP.ID_MOTIVO_ESCLUSO_PROCEDIMENTO
                                       FROM DB_MOTIVO_ESCLUSO_PROCEDIMENTO MEP
                                      WHERE DC.ID_MOTIVO_DICHIARAZIONE = MEP.ID_MOTIVO_DICHIARAZIONE
                                        AND MEP.ID_PROCEDIMENTO = knIdProcedimentoUma ));

     EXCEPTION WHEN NO_DATA_FOUND THEN
       P_CODERR := '1';
       P_MSGERR := 'NON ESISTE ALCUNA DICHIARAZIONE DI CONSISTENZA IN ANAGRAFE';
       N_ID_CONSISTENZA_GAA := NULL;
       RAISE ERRORE;
     WHEN OTHERS THEN
       P_CODERR := '1';
       P_MSGERR := 'ERRORE GRAVE ACCESSO DB_DICHIARAZIONE_CONSISTENZA. CONTATTARE L''ASSISTENZA COMUNICANDO LA SEGUENTE ANOMALIA: ' || SQLERRM;
     RAISE ERRORE;
   END;

   /*  Cerco qual  l'ultima importazione dati di superficie da Anagrafe */
   FOR recSupAz IN (
       SELECT EXT_ID_CONSISTENZA, DATA_CONSISTENZA, MAX(ID_SUPERFICIE_AZIENDA) ID_SUP_MAX
         FROM DB_SUPERFICIE_AZIENDA
        WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
          AND DATA_FINE_VALIDITA IS NULL
          AND DATA_SCARICO          IS NULL
        GROUP BY EXT_ID_CONSISTENZA, DATA_CONSISTENZA) LOOP

      N_ID_CONSISTENZA_UMA    := recSupAz.EXT_ID_CONSISTENZA;
      D_DATA_CONSISTENZA_UMA  := recSupAz.DATA_CONSISTENZA;
      N_ID_SUPERFICIE_AZIENDA := recSupAz.ID_SUP_MAX;

   /* Se non  stata trovata alcuna superficie importata da Anagrafe,
      cerco qual  l'ultima importazione dati di allevamenti da Anagrafe */
   IF N_ID_CONSISTENZA_UMA IS NULL THEN
         BEGIN
       SELECT EXT_ID_CONSISTENZA, DATA_CONSISTENZA, MAX(ID_ALLEVAMENTO)
         INTO N_ID_CONSISTENZA_UMA, D_DATA_CONSISTENZA_UMA, N_ID_ALLEVAMENTO
         FROM DB_ALLEVAMENTO
        WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
          AND DATA_FINE_VALIDITA IS NULL
          AND DATA_SCARICO          IS NULL
        GROUP BY EXT_ID_CONSISTENZA, DATA_CONSISTENZA;
      EXCEPTION
          WHEN NO_DATA_FOUND THEN
           N_ID_CONSISTENZA_UMA       := NULL;
         WHEN OTHERS THEN
             P_CODERR := '1';
          P_MSGERR := 'ERRORE GRAVE ACCESSO DB_ALLEVAMENTO. CONTATTARE L''ASSISTENZA COMUNICANDO LA SEGUENTE ANOMALIA: ' || SQLERRM;
        RAISE ERRORE;
      END;
    END IF;

   /* Se non  stata trovata alcuna superficie e allevamento importata da Anagrafe,
      cerco qual  l'ultima importazione dati di serre da Anagrafe */
   IF N_ID_CONSISTENZA_UMA IS NULL THEN
         BEGIN
       SELECT EXT_ID_CONSISTENZA, DATA_CONSISTENZA, MAX(ID_SERRA)
         INTO N_ID_CONSISTENZA_UMA, D_DATA_CONSISTENZA_UMA, N_ID_SERRA
         FROM DB_SERRA
        WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
          AND DATA_FINE_VALIDITA IS NULL
          AND DATA_SCARICO          IS NULL
        GROUP BY EXT_ID_CONSISTENZA, DATA_CONSISTENZA;
      EXCEPTION
          WHEN NO_DATA_FOUND THEN
           N_ID_CONSISTENZA_UMA       := NULL;
         WHEN OTHERS THEN
             P_CODERR := '1';
          P_MSGERR := 'ERRORE GRAVE ACCESSO DB_SERRA. CONTATTARE L''ASSISTENZA COMUNICANDO LA SEGUENTE ANOMALIA: ' || SQLERRM;
        RAISE ERRORE;
      END;
    END IF;

    IF N_ID_SUPERFICIE_AZIENDA IS NOT NULL OR N_ID_ALLEVAMENTO IS NOT NULL OR N_ID_SERRA IS NOT NULL THEN
        IF D_DATA_CONSISTENZA_UMA = D_DATA_CONSISTENZA_GAA AND nContDom = 0 THEN
           -- i dati sono uguali a quelli dell'ultima dichiarazione
           P_CODERR := '1';
           P_MSGERR := 'IMPORTAZIONE DEI DATI GIA'' EFFETTUATA';
           RAISE ERRORE;
        ELSE
            BEGIN
                 SELECT SUM(TOTALE) INTO N_CONTATORE FROM (
                 SELECT COUNT(*) TOTALE
                   FROM DB_SUPERFICIE_AZIENDA B, DB_COLTURA_PRATICATA C, DB_CARBURANTE_COLTURA D
                  WHERE B.ID_DITTA_UMA = P_ID_DITTA_UMA
                    AND B.DATA_FINE_VALIDITA IS NULL
                    AND B.DATA_SCARICO          IS NULL
                    AND C.ID_SUPERFICIE_AZIENDA = B.ID_SUPERFICIE_AZIENDA
                    AND C.ID_COLTURA_PRATICATA = D.ID_COLTURA_PRATICATA
                    AND B.EXT_ID_CONSISTENZA = N_ID_CONSISTENZA_UMA
                UNION
                 SELECT COUNT(*) TOTALE
                   FROM DB_ALLEVAMENTO B, DB_CARBURANTE_ALLEVAMENTO A
                  WHERE B.ID_DITTA_UMA = P_ID_DITTA_UMA
                    AND B.DATA_FINE_VALIDITA IS NULL
                    AND B.DATA_SCARICO          IS NULL
                    AND B.ID_ALLEVAMENTO = A.ID_ALLEVAMENTO
                    AND B.EXT_ID_CONSISTENZA = N_ID_CONSISTENZA_UMA
                UNION
                 SELECT COUNT(*) TOTALE
                   FROM DB_SERRA B, DB_CARBURANTE_SERRA A
                  WHERE B.ID_DITTA_UMA = P_ID_DITTA_UMA
                    AND B.DATA_FINE_VALIDITA IS NULL
                    AND B.DATA_SCARICO          IS NULL
                    AND B.ID_SERRA = A.ID_SERRA
                    AND B.EXT_ID_CONSISTENZA = N_ID_CONSISTENZA_UMA);
             EXCEPTION
             WHEN OTHERS THEN
               P_CODERR := '1';
               P_MSGERR := 'ERRORE GRAVE ACCESSO DB_SUPERFICIE_AZIENDA UNION DB_ALLEVAMENTO UNION DB_SERRA. CONTATTARE L''ASSISTENZA COMUNICANDO LA SEGUENTE ANOMALIA: ' || SQLERRM;
               RAISE ERRORE;
             END;
        END IF;
    END IF;
    -- se per la vecchia importazione non avevo effettuato
    -- l'assegnazione di carburante
     IF N_CONTATORE <= 0 AND N_ID_CONSISTENZA_UMA IS NOT NULL THEN
         -- posso effettuare la cancellazione di alcune tabelle
         DELETE FROM DB_PARTICELLA_COLTURA
          WHERE ID_COLTURA_PRATICATA IN  (SELECT ID_COLTURA_PRATICATA
                                                 FROM DB_COLTURA_PRATICATA A, DB_SUPERFICIE_AZIENDA B
                                          WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
                                            AND EXT_ID_CONSISTENZA = N_ID_CONSISTENZA_UMA
                                            AND B.DATA_FINE_VALIDITA IS NULL
                                            AND B.DATA_SCARICO          IS NULL
                                            AND A.ID_SUPERFICIE_AZIENDA = B.ID_SUPERFICIE_AZIENDA);

         DELETE FROM DB_COLTURA_PRATICATA
          WHERE ID_SUPERFICIE_AZIENDA IN (SELECT ID_SUPERFICIE_AZIENDA
                                                 FROM DB_SUPERFICIE_AZIENDA
                                          WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
                                               AND DATA_FINE_VALIDITA IS NULL
                                            AND DATA_SCARICO          IS NULL
                                            AND EXT_ID_CONSISTENZA = N_ID_CONSISTENZA_UMA);

         DELETE FROM DB_COMUNI_TERRENI
          WHERE ID_SUPERFICIE_AZIENDA IN (SELECT ID_SUPERFICIE_AZIENDA
                                                 FROM DB_SUPERFICIE_AZIENDA
                                          WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
                                            AND DATA_FINE_VALIDITA IS NULL
                                            AND DATA_SCARICO          IS NULL
                                            AND EXT_ID_CONSISTENZA = N_ID_CONSISTENZA_UMA);

         DELETE FROM DB_SUPERFICIE_AZIENDA
          WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
            AND DATA_FINE_VALIDITA IS NULL
            AND DATA_SCARICO IS NULL
            AND EXT_ID_CONSISTENZA = N_ID_CONSISTENZA_UMA;

         DELETE FROM DB_LAVORAZIONI_PRATICATE
          WHERE ID_ALLEVAMENTO IN (SELECT ID_ALLEVAMENTO
                                     FROM DB_ALLEVAMENTO A
                                    WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
                                      AND DATA_FINE_VALIDITA IS NULL
                                      AND DATA_SCARICO IS NULL
                                      AND EXT_ID_CONSISTENZA = N_ID_CONSISTENZA_UMA);

         DELETE FROM DB_ALLEVAMENTO
          WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
            AND DATA_FINE_VALIDITA IS NULL
            AND DATA_SCARICO IS NULL
            AND EXT_ID_CONSISTENZA = N_ID_CONSISTENZA_UMA;

         DELETE FROM DB_SERRA_RISCALDAMENTO SR
         WHERE SR.ID_SERRA IN (
         SELECT SE.ID_SERRA 
           FROM DB_SERRA SE
          WHERE SE.ID_DITTA_UMA = P_ID_DITTA_UMA
            AND SE.DATA_FINE_VALIDITA IS NULL
            AND SE.DATA_SCARICO       IS NULL
            AND SE.EXT_ID_CONSISTENZA = N_ID_CONSISTENZA_UMA );

         DELETE FROM DB_SERRA
          WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
            AND DATA_FINE_VALIDITA IS NULL
            AND DATA_SCARICO          IS NULL
            AND EXT_ID_CONSISTENZA = N_ID_CONSISTENZA_UMA;
    END IF;
   END LOOP;
    -- STORICIZZO I VECCHI DATI
    -- in realt se sono passato nell'IF precedente
    -- le update non faranno assolutamente nulla
    UPDATE DB_SUPERFICIE_AZIENDA SET DATA_FINE_VALIDITA = SYSDATE
     WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
       AND DATA_FINE_VALIDITA IS NULL;

    UPDATE DB_ALLEVAMENTO SET DATA_FINE_VALIDITA = SYSDATE
     WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
       AND DATA_FINE_VALIDITA IS NULL;

    UPDATE DB_SERRA SET DATA_FINE_VALIDITA = SYSDATE
     WHERE ID_DITTA_UMA = P_ID_DITTA_UMA
       AND DATA_FINE_VALIDITA IS NULL;

        IF NOT CARICA_TERRENI(N_ID_AZIENDA, P_ID_DITTA_UMA, N_ID_CONSISTENZA_GAA, D_DATA_CONSISTENZA_GAA,P_ID_UTENTE_AGGIORNAMENTO,P_IMPORTA_SOCI,1,P_MSGERR, P_CODERR) THEN
              RAISE ERRORE;
        END IF;

     -- aggiunto parametro N_ID_CONSISTENZA_UMA
     -- che contiene la vecchia dichiarazione di consistenza utilizzata nell'importazione DATI precedente
        IF NOT CARICA_ALLEVAMENTI(N_ID_AZIENDA, P_ID_DITTA_UMA, D_DATA_CONSISTENZA_GAA,N_ID_CONSISTENZA_GAA,P_ID_UTENTE_AGGIORNAMENTO, P_MSGERR, P_CODERR) THEN
              RAISE ERRORE;
        END IF;

        IF NOT CARICA_SERRE(N_ID_AZIENDA, P_ID_DITTA_UMA, D_DATA_CONSISTENZA_GAA,N_ID_CONSISTENZA_GAA, P_ID_UTENTE_AGGIORNAMENTO, P_MSGERR, P_CODERR) THEN
              RAISE ERRORE;
        END IF;

  IF NOT CaricaMacchineGaa(N_ID_AZIENDA,P_ID_DITTA_UMA,P_ID_UTENTE_AGGIORNAMENTO,P_MSGERR,P_CODERR) THEN
    RAISE ERRORE;
  END IF;


  nAnnoPar := TO_NUMBER(SelectValoreParametro('UMLC'));

  SELECT COUNT(*)
  INTO   nCont
  FROM   DB_DITTA_UMA DU,DB_ANAGRAFICA_AZIENDA AA,DB_TIPO_TIPOLOGIA_AZIENDA TTA
  WHERE  DU.ID_DITTA_UMA          = P_ID_DITTA_UMA
  AND    AA.ID_AZIENDA            = DU.EXT_ID_AZIENDA
  AND    AA.DATA_FINE_VALIDITA    IS NULL
  AND    AA.ID_TIPOLOGIA_AZIENDA  = TTA.ID_TIPOLOGIA_AZIENDA
  AND    TTA.FLAG_FORMA_ASSOCIATA = 'S'
  AND    TTA.ID_TIPOLOGIA_AZIENDA IN (4,5);

  IF TO_NUMBER(TO_CHAR(SYSDATE,'YYYY')) >= nAnnoPar AND nCont = 0 THEN
    PCK_SMRUMA_CARICA_LAVORAZ_CP.Main(P_ID_DITTA_UMA,
                                      P_ID_UTENTE_AGGIORNAMENTO,
                                      P_CODERR,
                                      P_MSGERR);
    IF P_CODERR = 1 THEN
      RAISE ERRORE;
    END IF;
  END IF;

  COMMIT;
exception
 when ERRORE then
   ROLLBACK;
 when others then
   P_CODERR := '1';
   P_MSGERR := 'ERRORE NON GESTITO: ' || sqlerrm;
   ROLLBACK;
END IMPORTA_DATI;

   /*********************************************************************
   Data una stringa SQL in input la esegue tramite EXECUTE IMMEDIATE
   Tipo:  funzione
   input: pSqlStr
   output: nessuno
   ritorno: 0 OK, 1 KO
   *********************************************************************/
   FUNCTION EseguiComandoSql (pSqlStr IN VARCHAR2) RETURN INTEGER IS
   BEGIN

        DBMS_OUTPUT.PUT_LINE ('Esecuzione stringa SQL : ' || SUBSTR(pSqlStr,1,200));
        DBMS_OUTPUT.PUT_LINE ('Inizio : ' || TO_CHAR(SYSDATE,'DD/MM/YYYY HH24:MI:SS'));

        EXECUTE IMMEDIATE pSqlStr;
        COMMIT;
        DBMS_OUTPUT.PUT_LINE ('Fine : ' || TO_CHAR(SYSDATE,'DD/MM/YYYY HH24:MI:SS'));
        DBMS_OUTPUT.PUT_LINE ('Esecuzione OK');
        RETURN 0;

   EXCEPTION
       WHEN OTHERS THEN
            DBMS_OUTPUT.PUT_LINE (TO_CHAR(SQLCODE) || ' - ' || SUBSTR(SQLERRM,1,150));
            DBMS_OUTPUT.PUT_LINE ('Fine : ' || TO_CHAR(SYSDATE,'DD/MM/YYYY HH24:MI:SS'));
            DBMS_OUTPUT.PUT_LINE ('Esecuzione KO');
            RETURN 1;
   END EseguiComandoSql;

END PACK_IMPORTA_DATI;

  -- Package
/
